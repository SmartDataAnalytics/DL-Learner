Some learning examples use background knowledge in *.kb files. These 
files use a DL-Learner internal convenience syntax and can be converted
to OWL. 

Here are some examples for axioms in this syntax:

person = (male OR female).
mother = (female AND EXISTS hasChild.TOP).
motherManyDaughters = (female AND >= 4 hasChild.female).
(mother AND father) SUBCLASSOF person.

This is the EBNF description of the input language [slightly outdated]:

Number = ["1"-"9"] (["0"-"9"])*
Id = ["a"-"z"] (["_","a"-"z","A"-"Z","0"-"9"])*
String: "\"" (~["\"","\\","\n","\r"])* "\""
Instruction =   ConfOption 
              | FunctionCall 
              | PosExample 
              | NegExample 
              | ABoxConcept 
              | ABoxRole 
              | Transitive 
              | Functional 
              | Symmetric 
              | Inverse 
              | Subrole 
              | TBoxEquiv 
              | TBoxSub
ConfOption = Id [ "." Id ] "="  ( Id | Number ) ";"
FunctionCall = Id "(" String ")" ";"
PosExample = "+" Id "(" Id ")" "."
NegExample = "-" Id "(" Id ")" "."
ABoxConcept = Concept "(" Id ")" "."
ABoxRole = Id "(" Id "," Id ")" "."
Transitive = "Transitive" "(" Id ")" "."
Functional = "Functional" "(" Id ")" "."
Symmetric = "Symmetric" "(" Id ")" "."
Inverse = "Inverse" "(" Id "," Id ")" "."
Subrole = "Subrole" "(" Id "," Id ")" "."
TBoxEquiv = Concept "=" Concept "."
TBoxSub = Concept ("SUBCLASSOF" | "SUB" ) Concept "."
Concept =   "TOP"
          | "BOTTOM"
          | Id
          | "(" Concept "AND" Concept ")"
          | "(" Concept "OR" Concept ")"
          | "EXISTS" Id "." Concept
          | "ALL" Id "." Concept
          | "NOT" Concept
          | ">=" Number Id "." Concept
          | "<=" Number Id "." Concept
